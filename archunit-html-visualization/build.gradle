plugins {
    id "com.github.node-gradle.node" version "3.0.1"
}

ext.moduleName = 'com.tngtech.archunit.htmlvisualization'

dependencies {
    api project(path: ':archunit')
    implementation dependency.guava
    implementation dependency.gson

    testImplementation dependency.log4j_api
    testImplementation dependency.log4j_core
    testImplementation dependency.log4j_slf4j
    testImplementation dependency.junit4
    testImplementation dependency.junit_dataprovider
    testImplementation dependency.mockito
    testImplementation dependency.assertj
    testImplementation(dependency.assertj_guava) {
        exclude module: 'assertj-core'
        exclude module: 'guava'
    }

    testImplementation project(path: ':archunit', configuration: 'tests')

    // This is a hack for running tests with IntelliJ instead of delegating to Gradle,
    // because for some reason this dependency cannot be resolved otherwise :-(
    testRuntimeOnly dependency.asm
}

shadowJar {
    exclude 'META-INF/maven/**'
}

def nodeVersion = '14.15.5'
def nodeDir = file("${project.projectDir}/nodejs")

node {
    version = nodeVersion
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = nodeDir
}

task setupNode(dependsOn: ['npmInstall']) {
    doLast {
        def subFolder = nodeDir.list().grep { it.contains(nodeVersion) }[0]
        println """
            >> Setting up NodeJs and running npm install...
            >> To run node from the command line, add the nodejs bin folder to your PATH:
            >> export PATH="${project.projectDir}/nodejs/${subFolder}/bin:\$PATH"
            """.stripIndent()
    }
}

task cleanNode(type: Delete) {
    delete 'nodejs', 'node_modules'
}

task cleanAll(dependsOn: [clean, cleanNode])

task mocha(type: NpmTask, dependsOn: 'setupNode') {
    args = ['run', 'test']
}

task eslint(type: NpmTask, dependsOn: 'setupNode') {
    args = ['run', 'lint']
}

def visualizationOutputDir = "${sourceSets.main.output.resourcesDir}/com/tngtech/archunit/htmlvisualization"

task createVisualizationOutputDir {
    doFirst {
        file(visualizationOutputDir).mkdirs()
    }
}

task webpack(type: NpmTask, dependsOn: ['mocha', 'eslint', 'setupNode', 'createVisualizationOutputDir']) {
    args = ['run', 'createJsBundle', '--', '--env.buildPath=' + visualizationOutputDir]
}

task bundleHtml(type: NpmTask, dependsOn: 'webpack') {
    args = ['run', 'bundleHtml',
            '--',
            '--redirect', 'build://|' + visualizationOutputDir + '/',
            '--out-file', visualizationOutputDir + '/visualization.html',
            'src/main/app/visualization.html']
}

task deleteJsBundles(type: Delete, dependsOn: 'bundleHtml') {
    delete fileTree(visualizationOutputDir) {
        include '**/*.js'
    }
}

task buildVisualization(dependsOn: ['mocha', 'eslint', 'webpack', 'bundleHtml', 'deleteJsBundles']) {}

test.dependsOn buildVisualization
assemble.dependsOn buildVisualization
